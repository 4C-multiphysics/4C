# ===============
# ||  VARIABLES   ||
# ===============
# { radius = 5.0 }
# { elsize_fine = 0.8}   # for higher accuracy, choose 0.4
# { elsize_coarse = 2.5 }
# { total_height = radius*6.5 }
# { head_width = radius*(1+(1-cosd(30))) }
# { coarseness = 6 }   must be between 1 and 9, default 5 is appropriate, 6 is faster, but still okay.

# ================
# ||  GEOMETRY  ||
# ================
# 2D shape
create surface rectangle width {radius}  height {5*radius} zplane
move surface 1 x {radius/2.0} y {radius*2.5} include_merged
create curve arc radius {radius} center location {2*radius} {5*radius} 0 normal 0 0 1 start angle 150 stop angle 180
create curve location 0 {5*radius} 0 location 0 {5*head_width} 0
create curve vertex 5 8
create surface curve 1 5 7 6
create curve location 0 {total_height} 0 location {head_width} {total_height} 0
create surface vertex 8 5 12 11
imprint tolerant surface 1 3 2
merge surface all

# quarter 3D
create curve arc radius {radius} center location 0 0 0 normal 0 1 0 start angle 0 stop angle 90
create curve vertex 3 18
create surface curve 3 14 15
sweep curve 4 5 11  yaxis angle 90
create curve vertex 29 11
create surface curve 28 12 29
create surface curve 29 25 21 17 15 2 6 13
create volume surface all heal
volume 9 redistribute nodes off
volume 9 scheme Sweep source surface 4 target surface 8 sweep transform least squares
volume 9 autosmooth target on fixed imprints off smart smooth off

# ===============
# ||  MESHING  ||
# ===============
curve 17 scheme bias fine size {elsize_fine} coarse size {elsize_coarse} start vertex 18
curve 2 scheme bias fine size {elsize_fine} coarse size {elsize_coarse} start vertex 3
curve 4 scheme bias fine size {elsize_fine} coarse size {elsize_coarse} start vertex 4
curve 25 13 11 interval 2
curve 25 13 11 scheme equal
curve 21 6 5 interval 3
curve 21 6 5 scheme equal
volume 9 size auto factor {coarseness}
mesh volume 9

# =================
# ||  NODE SETS  ||
# =================
nodeset 1 add surface 4
nodeset 1 name "YSYMM"
nodeset 2 add surface 1 2 3
nodeset 2 name "ZSYMM"
nodeset 3 add surface 9
nodeset 3 name "XSYMM"
nodeset 4 add surface 8
nodeset 4 name "TOP"

# ======================
# ||  DUMMY MATERIAL  ||
# ======================
set duplicate block elements off
block 1 add volume 9
block 1 element type hex27
create material "metal" property_group "CUBIT-ABAQUS"
modify material "metal" scalar_properties "MODULUS" 7e+04 "POISSON" 0.33
block 1 material 'metal'

# ====================
# ||  WRITE EXODUS  ||
# ====================
export mesh "tutorial_solid_geo.e"  overwrite


